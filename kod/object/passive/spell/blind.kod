% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Blind is Spell

constants:

   include blakston.khd

resources:

   blind_name_rsc = "blind"
   blind_icon_rsc = iblind.bgf
   blind_desc_rsc = \
      "The magical force of Qor burns the target's eyes, "
      "taking away all sight for a brief period.  "
      "Requires entroot berries and purple mushrooms to cast."
   
   blind_caster = "%s%s is now blind."
   blind_already_enchanted = "%s%s is already blind."

   blind_on = \
      "Something burns in your eyes, causing excruciating pain and a loss "
      "of vision."
   blind_off = "Your eyes begin to function again."
	blind_cannot_cast_yet = "You can use the blind spell again in %i seconds."
	blind_victim_immune_caster = "Your target has temporarily grown immune to vision impairing effects!"
	blind_victim_immune_target = "You have temporarily grown immune to vision impairing effects!"

   blind_sound = qblind.wav

classvars:

   vrName = blind_name_rsc
   vrIcon = blind_icon_rsc
   vrDesc = blind_desc_rsc

   viSpell_num = SID_BLIND
   viSchool = SS_QOR
   viSpell_level = 5
   viMana = 10
   viChance_To_Increase = 15
	viPostCast_time = 0

   viSpellExertion = 0

   viOutlaw = TRUE
   viHarmful = TRUE
   viNoNewbieOffense = TRUE

   vrSucceed_wav = blind_sound

properties:


messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&EntrootBerry,2],plReagents);
      plReagents = Cons([&PurpleMushroom,1],plReagents);

      return;
   }

   SetSpellPlayerFlag(who = $)
   {
      if NOT (IsClass(who,&DM) AND Send(who,@PlayerIsImmortal))
      {
         Send(who,@SetPlayerFlag,#flag=PFLAG_NO_FIGHT,#value=TRUE);
         Send(who,@SetPlayerFlag,#flag=PFLAG_NO_MAGIC,#value=TRUE);
      }
      
      return;
   }

   GetNumSpellTargets()
   {
      return 1;
   }

   CanPayCosts(who = $, lTargets = $, bItemCast = FALSE)
   {
      local target, i;
		
		% Can't cast if we are on internal cooldown.
		if Send(who,@CanCastBlind) <> $
      {
			Send(who,@MsgSendUser,#message_rsc=blind_cannot_cast_yet,#parm1=Send(who,@GetBlindCooldown)/1000);
         return FALSE;
      }
      
      % Can cast spell if the 1 target item is a user
      if Length(lTargets) <> 1
      {
         return FALSE;
      }

      target = First(lTargets);
      if (NOT IsClass(target,&Battler)) OR IsClass(target,&Revenant)
      {
         if NOT bItemCast
         {
            Send(who,@MsgSendUser,#message_rsc=spell_bad_target,#parm1=vrName,
                 #parm2=Send(target,@GetDef),#parm3=Send(target,@GetName));
         }

         return FALSE;
      }

      if target = who
      {
         if NOT bItemCast
         {
            Send(who,@MsgSendUser,#message_rsc=spell_no_self_target,#parm1=vrName);
         }
         
         return FALSE;
      }

      % check for enchantment already applied
      if Send(target,@IsEnchanted,#what=self)
      {
         if NOT bItemCast
         {
            Send(who,@MsgSendUser,#message_rsc=blind_already_enchanted,
	              #parm1=Send(target,@GetCapDef),#parm2=Send(target,@GetName));
         }
         
         return FALSE;
      }

      propagate;
   }

   CastSpell(who = $, lTargets = $, iSpellPower = 0)
   {
      local oTarget, iDuration;

      oTarget = First(lTargets);
		
		iDuration = Send(self,@GetDuration,#iSpellPower=iSpellPower,#target=oTarget);

      % Spell effects
      Send(self,@DoBlind,#who=who,#oTarget=oTarget,#duration=iDuration);
		
		% Internal Cooldown
		Send(who,@InternalCooldown,#spellid=viSpell_num);
      
      propagate;
   }

   DoBlind(who=$,oTarget=$,duration=0)
   {
      local iBlinded, oSpell;

		% Let's see if the spell is suffering from diminishing returns.
		iBlinded = Send(oTarget,@CanBeBlinded);
		
		if iBlinded > 0
		{
			duration = duration / 2;
			
			if iBlinded > 1
			{
				duration = duration / 2;
				
				if iBlinded > 2
				{
					% Target has grown immune to blind.
					if who <> $
					{
						Send(who,@MsgSendUser,#message_rsc=blind_victim_immune_caster);
					}
					Send(oTarget,@MsgSendUser,#message_rsc=blind_victim_immune_target);
					
					return;
				}
			}
		}
		
		% Increase the diminishing returns counter by 1.
		Send(oTarget,@DiminishingReturns,#spellid=viSpell_num);
		
		% Reduce duration based on eagle eyes.
      oSpell = Send(SYS,@FindSpellByNum,#NUM=SID_EAGLE_EYES);
      if Send(oTarget,@IsEnchanted,#what=oSpell)
      {
         duration = Send(oSpell,@DoEagleEyes,#oCaster=who,#oTarget=oTarget,#duration=duration);
      }

		if duration = 0
		{
			% Resisted completely.
			return;
		}

      % Bound
      duration = bound(duration,2000,$);
		
      Send(oTarget,@StartEnchantment,#what=self,#time=duration);

      if IsClass(oTarget,&Player)
      {
			% Do not blind DMs.
         if NOT (IsClass(oTarget,&DM) AND Send(oTarget,@PlayerIsImmortal))
         {
            Send(oTarget,@MsgSendUser,#message_rsc=blind_on);
            Send(oTarget,@EffectSendUser,#what=self,#effect=EFFECT_BLIND_ON);
         }
      }
      else  
      {
         % class is Monster
         Post(oTarget,@ResetBehaviorFlags);
      }
		
      if IsClass(who,&Player)
		{
			Send(who,@MsgSendUser,#message_rsc=blind_caster,#parm1=Send(oTarget,@GetCapDef),#parm2=Send(oTarget,@GetName));
		}

      return;
   }

   GetDuration(iSpellPower=0,target=$)
   {
      local iDuration;

      % 5 to 10 seconds based on spellpower and up to a 2 seconds bonus depending on the target's karma,
		% to compensate for low spellpower. Capped at 10 seconds.
      iDuration = 5000 + iSpellPower * 50 + 20 * bound(Send(target,@GetKarma,#detect=TRUE),0,100);
		iDuration = bound(iDuration,3000,10000);
		
      return iDuration;
   }
   
   EndEnchantment(who = $, report = TRUE)
   {
      if IsClass(who,&Player)
      {
         Send(who,@EffectSendUser,#what=self,#effect=EFFECT_BLIND_OFF);
         if report
         {
            Send(who,@MsgSendUser,#message_rsc=blind_off);
         }
      } 
      else  
      {
         % Class is monster.
         % This has to be done AFTER the enchantment is gone from the
         %  monster's ench list - post it.
         Post(who,@ResetBehaviorFlags);   
      }
     
      return;
   }

   SendEffectData()
   {
      return;
   }

   RestartEnchantmentEffect(who=$,state=$)
   {
      Send(who,@MsgSendUser,#message_rsc=blind_on);
		Send(who,@EffectSendUser, #what=self, #effect=EFFECT_BLIND_ON);
      
      return;
   }

   ModifyMonsterBehavior(mob = $)
   {
      Send(mob,@SetBehaviorFlag,#flag=AI_MOVE_RANDOM,#value=TRUE);
      Send(mob,@SetBehaviorFlag,#flag=AI_NOFIGHT,#value=TRUE);
      
      return;      
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
