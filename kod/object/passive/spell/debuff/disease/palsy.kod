% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Palsy is Disease

constants:

   include blakston.khd

	% This here is the emulated spellpower. With 100 SP, the resulting might loss is 20.
   PALSY_MIGHT_LOSS = 100
   PALSY_DURATION = 60000        % 1 minute

resources:

   palsy_name_rsc = "palsy"
   palsy_icon_rsc = ienfeebl.bgf
   palsy_desc_rsc = "Where once you were like a titan, now it seems you have the strength of a feeble old lady."

   palsy_start = "Your muscles turn to water."
   palsy_stop  = "You feel your old strength coming back."
   
classvars:

   vrName = palsy_name_rsc
   vrIcon = palsy_icon_rsc
   vrDesc = palsy_desc_rsc
   	
    viShow_enchantment_icon = 0x02

properties:

messages:

   GetSpellNum()
   {
      return SID_PALSY;
   }

   MakeSick(who = $,iDuration = PALSY_DURATION,iAmount = PALSY_MIGHT_LOSS, report = TRUE)
   {
      if IsClass(who,&Player)
      {
			Send(who,@StartEnchantment,#what=self,#state=send(self,@GetStateValue,#iSpellpower=iAmount,#target=who),#time=iDuration);
				
         if report
         {
            send(who,@MsgSendUser,#message_rsc=palsy_start);
         }
      }
      else
      {
         %%% class is &Monster
         Send(who,@StartPalsy);
         Send(who,@StartEnchantment,#what=self,#time=iDuration);
      }
      
      return;
   }
	
   GetStateValue(who = $, iSpellPower = 0, target = $)
   {
      if IsClass(target,&Player)
      {
			send(target,@AddMight,#points=-iSpellPower/5);
      }
      else
      {
         %%% class is &Monster
         Send(target,@StartPalsy);
         Send(target,@StartEnchantment,#what=self,#time=PALSY_DURATION);
      }

      return iSpellPower;
   }
	
   EndEnchantment(who = $,state = 0,report = TRUE)
   {
      if IsClass(who,&Player)
      {
			send(who,@AddMight,#points=state/5);
         if report
         {
            send(who,@MsgSendUser,#message_rsc=palsy_stop);
         }
      }
      else
      {
         %%% class is &Monster
         Send(who,@EndPalsy);
      }
      
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
