% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Poison is Debuff

constants:

   include blakston.khd

resources:

   poison_start = "Fresh poison courses through your veins like fire."
   poison_stop  = "The poison has run its course."
   poison_icon = iposoned.bgf
   poison_name = "poison"

   poison_kraanan = "~IKraanan's magic fortifies you against the accursed venom!"
   poison_desc = \
   "Like fire under your skin, the poison burns with dangerous "
   "strength, slowly sapping away at your strength, your health, "
   "and your will to live."

   poison_sound = poisoned.wav

classvars:

   vrIcon = poison_icon
   viShow_enchantment_icon = 0x02
   vrName = poison_name
   vrDesc = poison_desc

properties:

messages:

   GetSpellNum()
   {
      return SID_POISON;
   }

   MakePoisoned( who = $, lossrate = 0, duration = 0 )
   "Lossrate is in units of 10^-4 health points per second."
   "Duration is in milliseconds."
   {
      local oSpell;

      if not isClass(who,&Player)
      {
         return;
      }

      oSpell = send(SYS,@Findspellbynum,#num=SID_RESIST_POISON);
      if send(who,@isenchanted,#what=oSpell)
      {
         if (Random(1,100) < send(who,@GetEnchantedState,#what=oSpell))
         { 
            send(who,@MsgSendUser,#message_rsc=poison_kraanan);
            return; 
         }
      }

      send(who,@AddDamageOverTime,#strength=lossrate,#duration=duration,#what=self);
      send(who,@MsgSendUser,#message_rsc=poison_start);
      Send(who,@WaveSendUser,#wave_rsc=poison_sound);
      
      return;
   }

   DoTWearingOff(who=$,overtimeeffect=$,report=TRUE,delayed=FALSE)
   {
		if report
		{
			send(who,@MsgSendUser,#message_rsc=poison_stop);
		}
      
      return;
   }
   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
