% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
VampiricDrain is AttackSpell

constants:

   include blakston.khd

resources:

   VampiricDrain_name_rsc = "vampiric drain"
   VampiricDrain_icon_rsc = ivamptou.bgf
   VampiricDrain_desc_rsc = \
      "Draws out the very life force of the target and converts the energy into "
      "life for the caster.  Requires the blood of an Avar Shaman to cast."

   VampiricDrain_death = "You drain the last bit of %s%s's life force."
   VampiricDrain_health_gain = "You siphon your victim's life force, gaining ~g~B%i~B~n health."

   VampiricDrain_sound = qvtouch.wav

classvars:

   vrName = VampiricDrain_name_rsc
   vrIcon = VampiricDrain_icon_rsc
   vrDesc = VampiricDrain_desc_rsc

   viSchool = SS_QOR
   viSpell_num = SID_VAMPIRIC_DRAIN

   viSpell_level = 3
   viMana = 10
   viSpellExertion = 4

   viAttack_spell = ATCK_SPELL_ALL + ATCK_SPELL_UNHOLY

   vrSucceed_wav = VampiricDrain_sound

   viCast_delay = 0000
	viCast_time = 2000
	viDamage = 10
   viRange = 20
   
properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = cons([&ShamanBlood, 1],plReagents);
      
      return;
   }

   % The chance to do something in addition to normal damage.
   DoSideEffect(who=$,victim=$,damage=0)
   {
      local iToHeal;
      
      if who = $ OR victim = $
      {
         propagate;
      }

      % Take some of that damage and add it to our health!
      if damage <> $
      {
         % Heal for three times the damage dealt.
         iToHeal = bound(damage*3,1,$);
      }
      else
      {
         % Ah!  We killed someone.  Give them a littel bonus.
         iToHeal = bound(viDamage*4,1,$);
         Post(who,@MsgSendUser,#message_rsc=VampiricDrain_death,
              #parm1=Send(victim,@GetDef),#parm2=Send(victim,@GetName));
      }

      Send(who,@MsgSendUser,#message_rsc=VampiricDrain_health_gain,#parm1=iToHeal);
      Send(who,@GainHealth,#amount=iToHeal);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
