% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
WeapAttPurger is WeaponAttribute

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  This weapon has a chance to purge positive enchantments from the 
%     opponent every time it does damage.
%
%   Form is:
%
%      [ WA_PURGER]
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

constants:

   include blakston.khd

resources:

   weapattpurger_desc = " The sign of Psychochild is engraved on the weapon."
   purger_fail_use = "You are not yet ready to use %s%s."
   purger_worked_user = "Your weapon tears at the magic surrounding %s%s."
   purger_worked_target = "%s%s's weapon tears at the magic surrounding you!"
   purger_dm = "purger"

classvars:

   vrDesc = weapattpurger_desc
   vrDesc2 = $

   viItem_Att_Num = WA_PURGER

   viDifficulty = 9
   vrDM_trigger = purger_dm

   vbAppears_in_Happyland = FALSE

properties:

   piValue_modifier = 200   %% modify the object's price by 100%
   piValue_power_modifier = 10
   piEffect_percent = 15

messages:

   ItemReqUse(oItem=$,oPlayer=$)
   {
      % this person needs 50% in the weapon's proficiency to use
      if Send(oPlayer,@GetSkillAbility,#skill_num=Send(oItem,@GetProfNum)) < 50 
      {
         Send(oPlayer,@MsgSendUser,#message_rsc=purger_fail_use,
              #parm1=Send(oItem,@GetDef),#parm2=Send(oItem,@GetName));

         return FALSE;
      }

      return TRUE;
   }

   %%% Effect Functions

   ModifyDamage(who=$,what=$,stroke_obj=$,use_weapon=$,lData=$)
   "Wielder has a 1% chance of purging target with any hit."
   {
      local oSpell;

      if what = $
      {
         return 0;
      }

      if Random(1,100) < piEffect_percent
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_PURGE);
         
         %% Gotta have something to try to remove....
         if Send(what,@IsEnchanted)
         {
            % Try to purge off Random(25,75)% of the enchantments.
            if Send(oSpell,@DoPurge,#who=what,#iChance=Random(25,75))
            {
               % Only tell our victim if they lost enchantments.
               Send(what,@MsgSendUser,#message_rsc=purger_worked_target,
                     #parm1=Send(who,@GetCapDef),#parm2=Send(who,@GetName));
            }

            % Tell the wielder every time we fired so they think it's cool.
            Send(who,@MsgSendUser,#message_rsc=purger_worked_user,
                  #parm1=Send(what,@GetCapDef),#parm2=Send(what,@GetName));
         }
      }

      return 0;
   }

   IsMagicalEffect()
   {
      return TRUE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%